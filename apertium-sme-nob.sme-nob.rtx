! sme-nob.rtx

! Pick genders etc. from tl (and if not, from anaphoric referent), but don't fall back to sl:
SIDE_SOURCES = tl ref ;

!!!!!!!!!!!!!!!!!!!!!!!!!!
!! ATTRIBUTE CATEGORIES !!
!!!!!!!!!!!!!!!!!!!!!!!!!!

number = sg pl ;
gender = f m nt un mf ;
defnes = (DD ind) def ind DD ;
comp = pst comp sup ;
sint = sint ;
prn_type = pers dem refl pos ; ! pos possessive
person = p1 p2 p3 ;
case = nom acc gen ill loc ess ;
oblcase = acc gen ill loc ess ;
advlcase = ill loc ess ;
semtxt = sem_ani_build_hum_txt sem_ani_plc_txt sem_group_txt sem_mat_txt sem_money_txt sem_org_txt sem_plc_txt sem_prod-audio_txt sem_prod-cogn_txt sem_semcon_txt sem_txt ;
andretag = sem_hum ;
semtag = [semtxt] [andretag]  ;
transitivity = tv iv ;
tense = pres pret imp ;
vbinfin = inf prfprc prsprc ;
infl = [tense] [vbinfin] ;
!infl = pres pret imp ind prfprc prsprc ;
npclass = ant org al top geo;

a_det = qnt emph dem itg pos ;
a_cmp = cmp cmp-split ;
a_split = cmp-split ;


!!!!!!!!!!!!!!!!!!!!!!!
!! TAG-REWRITE RULES !!
!!!!!!!!!!!!!!!!!!!!!!!


!!!!!!!!!!!!!!!!!!!!!
!! OUTPUT PATTERNS !!
!!!!!!!!!!!!!!!!!!!!!

n: _.gender.number.defnes;
np: _.npclass.gender;
adj_plain: _.sint.comp.gender.number.defnes;
adj: (always
       1(adj_plain)[gender=(if (1.defnes = def or 1.number = pl) un
              elif (1.gender = f or 1.gender = m) mf
              else 1.gender), 
              number=(if (1.defnes = def) sp else 1.number)]);
adv: _;
num: _; ! fungerer ikkje
vblex: _.infl ;
post: _ ;
pr: _;
cnjcoo: _;
cnjsub: _;
part: _;

prn_pos: _.<pos>.gender.number;
prn_oth: _.prn_type.person.gender.number.case ;
!prn:     _.prn_type.person.gender.number.case ;
prn: (if (1.case/sl = gen) 1(prn_pos)
      else 1(prn_oth)) ;

! For referanse
!np_ant: _.<ant>.gender.number.case ;!
!np_other: _.np_type.number.case ;
!np: (if (1.np_type = ant) 1(np_ant)
!      else 1(np_other)) ;


!adj_attr: _.attr ;
!adj_pred: _.comp.number.case ;
!adj: (if (1.attr = attr) 1(adj_attr)
!      else 1(adj_pred)) ;


NP:    _.semtag.gender.number.case.defnes.a_split; ! NP needs semtag from below.
DP:    _.gender.number.case.defnes.a_split;
QP: _;
PP: _ ;
PROPN: _.npclass.gender;
VP: _ ;
ADV: _;
AP: _;
S: _ ;
C: _; ! cnjcoo
PR: _; ! pr

det: _.a_det.gender.number ;

! Mun oainnán girjji -> Jeg ser *ei* bok
! Mun oainnán stuorra girjjiid rukses viesu duohken.
! -> Jeg ser store bøker bak *det* røde huset.
has_adj = yes no ;

maybe_det: (if (1.defnes = def and 1.number = sg and 1.has_adj = yes)
               [ den@det.dem.[1.gender].sg _ ]
            elif (1.defnes = ind and 1.number = sg)
               [ en@det.qnt.[1.gender].sg _ ]
            elif (1.defnes = ind and 1.case/sl = acc)
               [ en@det.qnt.[1.gender].sg _ ]
            else []
            ) ;
! <def-attr n="has_adj"><attr-item n="yes"/><attr-item n="no"/></def-attr>



!!!!!!!!!!!!!!!!!!!!!
!! REDUCTION RULES !!
!!!!!!!!!!!!!!!!!!!!!

det -> %det.* {1};

ADV -> adv {1};
AP -> %adj {%1};
PROPN -> %np.* {%1} ; ! remove number and case 


! PR -> pr {1} ;

ess_i = "tillegg" "skumring" "regn" "tåke" ;
ess_på = "besøk" ;
ess_til = "aske" "grunn" "nytte" ;

ill_i = "arrest" "bruk" "del" "do" "fengsel" "forord" "gang" "tale" ;
ill_på = "ferie" "golv" "måte" "plass" "utveksling" ;

loc_av = "rein" "slekt" ;
loc_etter = "oppfatning" "mening" "syn" ;
loc_fra = "ønske" ;
loc_i = "Guovdageainnu suohkan" "NSR" "Nilut Cup" "Nord-Norge" "Norge" "USA" "administrasjon" "akkusativ" "alder" "alkoholrus" "allmenning" "analyse" "april" "arbeid" "arrest" "august" "avgrensning" "avsnitt" "avtale" "balanse" "barnehage" "barnehagetilbud" "barnevern" "bedrift" "befolkning" "begrep" "begynnelse" "besittelse" "biltrafikk" "bilulykke" "blod" "bok" "brann" "brev" "bruk" "bryting" "budsjett" "by" "bygd" "debatt" "departement" "desember" "dialog" "distrikt" "divisjon" "dokument" "dråpe" "eksempel" "enkeltsak" "erklæring" "essiv" "etableringsfase" "ettertid" "fag" "fagkrets" "faksimile" "fattigdom" "februar" "fellesskap" "fengsel" "film" "finale" "fiske" "fjord" "fjøs" "flaske" "flerspråklighet" "flertall" "flokk" "fond" "forbindelse" "form" "forord" "forskning" "forskrift" "forslag" "forvaltning" "forvaltningslov" "forvaltningsområde" "fosterhjem" "framtid" "frykt" "fylke" "fylkeskommune" "førskolealder" "første omgang" "gamme" "gang" "genitiv" "gjennomføring" "grunnform" "grunnskole" "grunnskolealder" "gruppe" "hage" "handlingsplan" "helsetjeneste" "hemmelighet" "historie" "hovedsak" "hjerte" "hus" "husstand" "hverdag" "idiolekt" "idrett" "illativ" "innstilling" "jakt" "januar" "jordbruk" "jordbruksarbeid" "juli" "juni" "kamp" "kapittel" "kirke" "klasse" "klasserom" "komité" "komítativ" "kommune" "kommunikasjon" "konvensjon" "kondisjonalis" "konjunktiv" "korpus" "kraft" "krav" "krets" "kristendom" "krukke" "kulde" "kultur" "kulturantropologi" "kulturminnelov" "kunst" "land" "landbruk" "lavvo" "ledelse" "ledelse" "lesing" "liv" "live" "lokalmiljø" "lokalsamfunn" "lokativ" "lov" "lovverk" "lys" "læreplan" "læreplanverk" "lærerutdanning" "mai" "maktposisjon" "mandat" "manntall" "mars" "matematikk" "medisin" "melding" "mellomrom" "meningsmåling" "mindretall" "mobiltelefon" "motvind" "mørke" "natur" "naturfag" "naust" "navn" "nominasjon" "nominativ" "nord" "nordområde" "november" "næring" "nærmiljø" "offentlighet" "oktober" "område" "omsorgsbolig" "oppfølging" "opplæring" "organisasjon" "paragraf" "partitiv" "pedagogikk" "periode" "perspektiv" "plan" "planarbeid" "plenum" "posisjon" "potesialis" "praksis" "presens" "preteritum" "prinsipp" "produksjon" "program" "proposisjon" "prosent" "prosess" "prosjekt" "prosjektperiode" "prosjektstønad" "protokoll" "rapport" "regjering" "region" "reindrift" "reindriftslov" "relasjon" "retningslinje" "rom" "rovdyrsak" "rus" "rør" "røving" "råd" "sak" "samarbeid" "samarbeidsavtale" "samband" "samelov" "sametingsråd" "samfunn" "samfunnsplanlegging" "samhandling" "samisk språk" "sammenheng" "samtale" "sending" "seng" "sentrum" "september" "serie" "seriefotball" "siida" "sjanger" "skap" "skimesterskap" "skirenn" "skriveopplæring" "skolealder" "skoleverk" "skuff" "speil" "spesialpedagogikk" "spill" "sak" "stat" "statsbudsjett" "statsråd" "stilling" "stillingskode" "storsamfunn" "stortingsmelding" "strid" "studie" "studium" "styre" "stue" "sum" "system" "søknad" "sør" "tabell" "tekst" "telefon" "teori" "tet" "tillegg" "tidsrom" "torvgamme" "trafikk" "trygghet" "tvil" "ulykke" "undervisning" "urfolkssak" "utdanning" "utdanning" "utdanningssak" "utgangspunkt" "utkast" "utmark" "utland" "utredning" "utvalg" "utvikling" "valgprogram" "valgår" "vask" "vedtak" "vegkant" "verden" "vest" "vidde" "vielse" "virkeområde" "vind" "virkelighet" "virksomhet" "votering" "år" "årsmelding" ;
loc_om = "kveld" "dag" "morgen" "minne" "natt" "tema" ;
loc_på = "Andøya" "Bygdøy" "Cuba" "Fagseminar for samisk statistikk" "Filippinene" "Gjøvik" "Grønland" "Hamar" "Hidra" "Hispaniola" "Hitra" "Hovedøya" "Island" "Kautokeino bygdetun" "Kola" "Komsa" "Kreta" "Kvaløya" "Kvaløysletta" "Lille Komsa" "Lillehammer" "Madagaskar" "Márkomeannu festivála" "Porsangmoen" "Ráisduottarháldi" "Sameskolen" "Samisk høgskole" "Sennalandet" "Sicilia" "Statnett" "Svalbard" "Sørøya" "Twitter" "UiT" "Varangerhalvøya" "Vestlandet" "Haldi" "arbeidsplass" "bachelornivå" "bilde" "bygdetun" "dag" "dato" "deltid" "fakultet" "fest" "festival" "fotballkamp" "fredag" "gårdsplass" "heltid" "hjemmebane" "hjemmeside" "hotell" "høyskole" "kino" "kinokveld" "kjøkken" "kort tid" "kveld" "kyst" "liste" "lørdag" "mandag" "marked" "møte" "nivå" "onsdag" "papir" "parkeringsplass" "plass" "sameskole" "scene" "side" "skole" "slette" "sted" "sykehjem" "sykehus" "søndag" "tall" "tirsdag" "topp" "torsdag" "tun" "universitet" "vegg" "Østlandet" "øy" ;
loc_per = "person" ;
loc_som = "verdi" ;
loc_ved = "makt" ;

noun_pseudo_prop = "april" "asyl" "august" "bachelornivå" "bestefar" "bestemor" "deltid" "desember" "doktorgrad" "downs syndrom" "duodji" "far" "februar" "fokus" "fredag" "gang" "giellasiida" "godkjenning" "grad" "gras" "gud" "gymnastikk" "heltid" "hjelp" "januar" "julaften" "juli" "juni" "junior" "kildinsamisk" "kofte" "kunst" "langfredag" "legesjekk" "lulesamisk" "lunsj" "lykke" "lørdag" "mai" "mandag" "mars" "mat" "merverdiavgift" "midnatt" "moms" "mor" "naturfag" "nivå" "nordsamisk" "november" "null" "oktober" "onsdag" "pop" "påskeaften" "raushet" "reinlav" "rektor" "rikdom" "rus" "samisk" "samiskhet" "senior" "sentrum" "september" "skjærtorsdag" "støtte" "søndag" "sørsamisk" "taekwondo" "takk" "tid" "tirsdag" "torsdag" "trygghet" "twitter" "vare" "velkommen" "verden" "viten" "yrkesfag" ;


PP -> NP post     1:                      {2 _ 1[defnes=(if (1.lem in_caseless noun_pseudo_prop) ind else def)]}
    | pr NP       1:                      {1 _ 2[defnes=(if (1.lem in_caseless noun_pseudo_prop) ind else def)]} 
    | "loc i"     5:     [loc_i]@NP.*.loc {  i@pr _ %1[defnes=(if (1.lem in_caseless noun_pseudo_prop) ind else def)]} 
    | "loc etter" 5: [loc_etter]@NP.*.loc {etter@pr _ %1[defnes=(if (1.lem in_caseless noun_pseudo_prop) ind else def)]} 
    | "loc om"    5:    [loc_om]@NP.*.loc { om@pr _ %1[defnes=(if (1.lem in_caseless noun_pseudo_prop) ind else def)]} 
    | "loc per"   5:   [loc_per]@NP.*.loc {per@pr _ %1[defnes=ind]} 
    | "loc som"   5:   [loc_som]@NP.*.loc {som@pr _ %1[defnes=ind]} 
    | "loc på"    5:     NP.*.loc         { på@pr _ %1[defnes=(if (1.lem in_caseless noun_pseudo_prop) ind else def)]}
    | "ill i"     5:  NP.*.[semtxt].*.ill {  i@pr _ %1[defnes=(if (1.lem in_caseless noun_pseudo_prop) ind else def)]}
    | "ill til"   5:     NP.*.ill         {til@pr _ %1[defnes=(if (1.lem in_caseless noun_pseudo_prop) ind else def)]}
    | "ess som"   5:     NP.*.ess         {som@pr _ %1[defnes=(if (1.lem in_caseless noun_pseudo_prop) ind else def)]}
    | "com med"   5:     NP.*.com         {med@pr _ %1[defnes=(if (1.lem in_caseless noun_pseudo_prop) ind else def)]}
    ;

!Forthcoming fix for coordination, today three phrases NP C NP:
!NP -> NPu
!    | NPu C NPu
!    ;
! The NP rule as NPu "under-NP"?


! Opne spørsmål:
! Vil vi ha NP = [NP cncjoo NP] som frase? Skal vi i tilfelle ha 
! NP = [NPu C NPu] (der NPu er underordna NP eller noko slikt?)


NP -> 0:    %n {1(maybe_det)[defnes=$defnes,has_adj=no]  %1 }
    | 3: %n.*.nom {%1[defnes=ind,has_adj=no]  }
    | 4: adj %n.*.nom {den@det.dem.m.sg _ 1[defnes=def] _ %2[defnes=def,has_adj=no]  }
!   | 3: %n.*.[advlcase] {%1[defnes=def,has_adj=no]  }
    | 3: %n.*.acc {1(maybe_det)[defnes=ind,has_adj=no]  %1 }
    | 2: adj %n [$defnes=ind]  
         { 2(maybe_det)[defnes=$defnes,has_adj=yes] 1[number=2.number,gender=2.gender,defnes=$defnes] _ %2}
    | 1: %prn.pers [$defnes=def] { %1 }
    | 2: %prn.dem { %1 }
    | 2: det %n [$defnes=ind] { 1 _ 2 }
    ;

! Denne fungerer ikkje
QP ->  "QP qnt"    8: num {%1}
    |  "QP qnt NP" 9: num NP.sg.gen { %1 _ 2[number=pl, defness=ind]}
    ;

C -> cnjcoo {1} 
   | cnjsub {1}
   ;

!NP -> %NP cnjcoo NP {1 _ 2 _ 3 } ;

!! Verb phrase

! Verbs demanding oblique complements

acc_om_verb = "be" "drømme" ; ! i VP-regelen?

ill_av_verb = "bealkkáhallat" "borahallat" "bovttáhallat" "cábmohallat" "cápmahallat" "dolkat" "dorohallat" "fillehallat" "gáskkáhallat" "gávnnahallat" "oainnahallat" "oainnahallat" "rissehallat" "vuojáhallat" ;
ill_for_verb = "leat" "láhčit" "mearkkašit" "čiegadit" ;
ill_i_verb = "ásahit" "bisánit" "bálkestit" "coggat" "investeret" "nahkehit" "oassálastit" "čiekŋudit" "čoahkkanit" "čuollat" "čuolastit" ;
ill_med_verb = "duhtat" "oahpásmuvvat" ;
ill_på_verb = "báhcit" "dorvvastit" "geahččat" "guovlat" "jáhkkit" "luohttit" "reageret" "searvat" "suhttat" "čoalkalit" "čáhkadit" ;

loc_for_verb = "nohkkot" "váruhit" ;
loc_fra_verb = "beassat" "boahtit" "earuhit" "fárret"  "gáidat" ;
loc_om_verb = "beroštit" ;
loc_som_verb = "beroštit" ;

! Case governed bŧ previous N

gen_av_head = "evaluering" "forfatter" "forskning" "kopi" "leder" "vurdering" ;
gen_for_head = "basis" "forening" "fundament" "grunnlag" "senter" "sjef" "søknadsfrist" ;
gen_i_head = "2018" "opplæring" "undervisning" "vårsemester" ;
gen_om_head = "fortelling" "sang" ;
gen_på_head = "måte" ;
gen_som_head = "masterutdanning" ;

ill_for_head = "behov" "forskningsgruppe" "interesse" "plan" "senter" ;
ill_i_head = "progresjon" ;
ill_på_head = "dagskvote" "innflytelse" "påvirkning" "sesongkvote" "syn" "tro" ;

loc_av_head = "NUMERAL" "del"  "flertall" "halvpart" "mindretall" "resultat" "sammendrag" ;
loc_etter_head = "mening" "oppfatning" ;
loc_for_head = "DATE" "frykt" "glad" "redd" "tema" ;
loc_fra_head = "ønske" "lykkeønskning" "hilsning" "kondolanse" ;
loc_i_head = "infeksjon" ;
loc_om_head = "artikkel" "avtale" "bok" "kontrakt" "snakk" "tale" ;

cond_NIL_verb = "berret" "dáhttut" "dáidit" "fertet" "galgat" "háliidit" "máhttit" "soaitit" "sáhttit" "veadjit" "áigut" ;

VP -> "enkeltverb"      %vblex                                {1 } 
    |       "ikke"      %ii@vblex.iv.neg vblex.*.conneg       {2 _ ikke@adv} 
    |                3: %[cond_NIL_verb]@vblex VP             {1 _ 2}    
    |  "V V inf"     3: %[cond_NIL_verb]@vblex vblex          {1 _ 2}    
    |  "V V inf"     2:                 %vblex vblex.*.inf    {1 _ å@part _ 2}    
    |  "V NP PP"     2:                 %vblex vblex.*.prfprc {1 _ 2}    
    |  "V NP PP"     2:                 %vblex PP             {1 _ 2}    
    |  "V NP PP"     3:                 %vblex NP PP          {1 _ 2 _ 3}    
    |  "V NP.ind"    5: %leat@vblex    NP.*.sg.nom    { 1 _ en@det.qnt.m.sg _ 2[defnes=ind] } ! gender fro NP!
!   |  "V NP.ind"    5: %leat@vblex NP.*.sg    { 1 _ en@det.qnt.[$gender=2.gender].sg _ 2[defnes=ind] } ! gender fro NP!
    |  "V NP.ind"    4: %vblex NP    { 1 _ 2[defnes=ind] }
    |                3: %vblex NP PP { 1 _ 2[defnes=ind] _ 3}    
    | "V adj" 1: %leat@vblex adj {%1 _ 2[gender=nt]}
! Verbs with oblique complements
    | "acc_om_verb"  2:  %[acc_om_verb]@vblex NP.*.acc { 1 _  om@pr _ 2[defnes=def] }
    | "ill_av_verb"  2:  %[ill_av_verb]@vblex NP.*.ill { 1 _  av@pr _ 2[defnes=def] }
    | "ill_i_verb"   2:   %[ill_i_verb]@vblex NP.*.ill { 1 _   i@pr _ 2[defnes=def] }
    | "ill_for_verb" 2: %[ill_for_verb]@vblex NP.*.ill { 1 _ for@pr _ 2[defnes=def] }
    | "ill_med_verb" 2: %[ill_med_verb]@vblex NP.*.ill { 1 _ med@pr _ 2[defnes=def] }
    | "ill_på_verb"  2:  %[ill_på_verb]@vblex NP.*.ill { 1 _  på@pr _ 2[defnes=def] }
    | "loc_for_verb" 2: %[loc_for_verb]@vblex NP.*.loc { 1 _ for@pr _ 2[defnes=def] }
    | "loc_fra_verb" 2: %[loc_fra_verb]@vblex NP.*.loc { 1 _ fra@pr _ 2[defnes=def] }
    | "loc_om_verb"  2:  %[loc_om_verb]@vblex NP.*.loc { 1 _  om@pr _ 2[defnes=def] }
    | "loc_som_verb" 2: %[loc_som_verb]@vblex NP.*.loc { 1 _ som@pr _ 2[defnes=def] }
    
    | "ill_for_verb" 4: %[ill_for_verb]@vblex AP  NP.*.ill { 1 _ 2 _ for@pr _ 3[defnes=def] } 
      ! TODO: Jeg betyr for den mye byen.
      ! NP1.gen NP2 > NP2 av NP1  ! girjji kopiija > en kopi av boka
    | "gen_av_head"    3: %vblex  NP.*.gen   [gen_av_head]@NP { 1 _ 3 _   av@pr _ 2[defnes=def] } 
    | "gen_for_head"   3: %vblex  NP.*.gen  [gen_for_head]@NP { 1 _ 3 _  for@pr _ 2[defnes=def] } 
    | "gen_i_head"     3: %vblex  NP.*.gen    [gen_i_head]@NP { 1 _ 3 _    i@pr _ 2[defnes=def] } 
    | "gen_om_head"    3: %vblex  NP.*.gen   [gen_om_head]@NP { 1 _ 3 _   om@pr _ 2[defnes=def] } 
    | "gen_på_head"    3: %vblex  NP.*.gen   [gen_på_head]@NP { 1 _ 3 _   på@pr _ 2[defnes=def] } 
    | "gen_som_head"   3: %vblex  NP.*.gen  [gen_som_head]@NP { 1 _ 3 _  som@pr _ 2[defnes=def] } 
      ! NP1 NP2.ill > NP1 for NP2 infekšuvdna gieđas > infeksjon i hånda
    | "ill_for_head"   3: %vblex   [ill_for_head]@NP NP.*.ill { 1 _ 2 _   for@pr _ 3[defnes=def] } 
    | "ill_i_head"     3: %vblex     [ill_i_head]@NP NP.*.ill { 1 _ 2 _     i@pr _ 3[defnes=def] } 
    | "ill_på_head"    3: %vblex    [ill_på_head]@NP NP.*.ill { 1 _ 2 _    på@pr _ 3[defnes=def] } 
    | "loc_av_head"    3: %vblex    [loc_av_head]@NP NP.*.loc { 1 _ 2 _    av@pr _ 3[defnes=def] } 
    | "loc_etter_head" 3: %vblex [loc_etter_head]@NP NP.*.loc { 1 _ 2 _ etter@pr _ 3[defnes=def] } 
    | "loc_for_head"   3: %vblex   [loc_for_head]@NP NP.*.loc { 1 _ 2 _   for@pr _ 3[defnes=def] } 
    | "loc_fra_head"   3: %vblex   [loc_fra_head]@NP NP.*.loc { 1 _ 2 _   fra@pr _ 3[defnes=def] } 
    | "loc_i_head"     3: %vblex     [loc_i_head]@NP NP.*.loc { 1 _ 2 _     i@pr _ 3[defnes=def] } 
    | "loc_om_head"    3: %vblex    [loc_om_head]@NP NP.*.loc { 1 _ 2 _    om@pr _ 3[defnes=def] } 
!!    !  Mun čálán čoahkkáigeasu girjjis > Jeg skriver et sammendrag av boka
!!VP? |                            [acc_om]NP.*.acc {om@pr _ 1[defnes=def]}
!!VP? |                            [acc_om]NP.*.acc {om@pr _ 1[semtag=semtxt]}
   ;

! !VP -> vblex.tv NP { 1 _ 2[defnes=ind] } ; ! Trond
! S -> NP VP { 1 _ 2 } ;

