! This file turns
!    vuođđogeađggit  vuođđu+N+SgNomCmp#geađgi+N+Pl+Nom
!    vuođđogeađggit  vuođđo#geađgi+N+Pl+Nom
! into
!    vuođđogeađggit  vuođđu+N+SgNomCmp#geađgi+N+Pl+Nom
!    vuođđogeađggit  vuođđogeađgi+N+Pl+Nom
! and
!    sámegiela       sápmi+SgGenCmp+N+SgGenCmp#giella+N+Sg+Gen
!    sámegiela       sápmi+SgGenCmp+N+SgGenCmp#giella+N+Sg+Acc
!    sámegiela       sápmi+SgGenCmp+PlGenCmp+N+SgGenCmp#giella+N+Sg+Gen
!    sámegiela       sápmi+SgGenCmp+PlGenCmp+N+SgGenCmp#giella+N+Sg+Acc
! into
!    sámegiela       sápmi+N+SgGenCmp#giella+N+Sg+Gen
!    sámegiela       sápmi+N+SgGenCmp#giella+N+Sg+Acc

Alphabet

 a b c d e f g h i j k l m n o p q   ! small
 r s t u v w x y z å ä ö ø æ %-
 á é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý þ ñ ð ß ª ß ç

 A B C D E F G H I J K L M N O P Q   ! capital
 R S T U V W X Y Z Å Ä Ö Ø Æ
 Á É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð

 č đ ŋ š ŧ ž          ! Sámi letters.
 Č Đ Ŋ Š Ŧ Ž          !

%#:0
%# ! Need this, since otherwise #-words not matched by our rule will disappear

%+N
%+Actor
%+A
%+Attr

%+Cmpnd%+
%+Cmpnd%+:0

%+Cmp
%+ShCmp
%+Cmpnd
%+SgCmp      	%+PlCmp
%+SgLeft      	%+PlLeft
%+SgNomCmp   	%+PlNomCmp
%+SgNomLeft  	%+PlNomLeft
%+SgGenCmp   	%+PlGenCmp
%+SgGenLeft  	%+PlGenLeft
%+DefSgGenCmp 	%+DefPlGenCmp

%+Cmpnd:0
%+SgCmp:0    	%+PlCmp:0
%+SgLeft:0    	%+PlLeft:0
%+SgNomCmp:0 	%+PlNomCmp:0
%+SgNomLeft:0  	%+PlNomLeft:0
%+SgGenCmp:0 	%+PlGenCmp:0
%+SgGenLeft:0  	%+PlGenLeft:0
%+DefSgGenCmp:0 %+DefPlGenCmp:0

;


Sets

Alphabetic =
 a b c d e f g h i j k l m n o p q   ! small
 r s t u v w x y z å ä ö ø æ %-
 á é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý þ ñ ð ß ª ß ç

 A B C D E F G H I J K L M N O P Q   ! capital
 R S T U V W X Y Z Å Ä Ö Ø Æ
 Á É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð

 č đ ŋ š ŧ ž          ! Sámi letters.
 Č Đ Ŋ Š Ŧ Ž          !
;

Cmptag = 
%+Cmp
%+ShCmp
%+Cmpnd
%+SgCmp      	%+PlCmp
%+SgLeft      	%+PlLeft
%+SgNomCmp   	%+PlNomCmp
%+SgNomLeft  	%+PlNomLeft
%+SgGenCmp   	%+PlGenCmp
%+SgGenLeft  	%+PlGenLeft
%+DefSgGenCmp 	%+DefPlGenCmp
;

RemCmptag = 			! don't remove Cmp and ShCmp
%+Cmpnd
%+SgCmp      	%+PlCmp
%+SgLeft      	%+PlLeft
%+SgNomCmp   	%+PlNomCmp
%+SgNomLeft  	%+PlNomLeft
%+SgGenCmp   	%+PlGenCmp
%+SgGenLeft  	%+PlGenLeft
%+DefSgGenCmp 	%+DefPlGenCmp
;

RemWICmptag =
%+Cmpnd%+
;

KeepCmpAfter =			! could expand to include lemmas...
%+N
%+Actor
%+A
%+Attr
;

Rules 

"#-deletion after alphabetic characters"
%#:0 <=> Alphabetic _ ;  

! After inversion, we get
!    ^sámegiella/sápmi+N+SgGenCmp#giella+N+Sg+Nom/sáme#giella+N+Sg+Nom$
! After this file, we get
!    ^sámegiella/sápmi+N+SgGenCmp#giella+N+Sg+Nom/sámegiella+N+Sg+Nom$
! After substitution/relabelling, we get
!    ^sámegiella/sápmi<N><Sg><Gen><Cmp>+giella<N><Sg><Nom>/sámegiella<N><Sg><Nom>$
! which after pretransfer turns into 
!    ^sápmi<N><Sg><Gen><Cmp>$ ^giella<N><Sg><Nom>$
!    ^sámegiella<N><Sg><Nom>$


"Delete compound tags (except Cmp/ShCmp) that weren't followed by an N or A tag"
RemCmptag:0 <=> \KeepCmpAfter _ ; 
! We don't remove +Cmp or +ShCmp tags since they follow lemmas, not +N or +A:
! mielbargin      miel+ShCmp#bargat+V+TV+Der2+Actor+N+Ess

"Delete compound tags at the beginning of words"
RemWICmptag:0 <=> [ # | #: ] _ ; 
