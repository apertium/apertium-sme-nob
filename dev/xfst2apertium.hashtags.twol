! This file turns
!    vuođđogeađggit  vuođđu+N+SgNomCmp#geađgi+N+Pl+Nom
!    vuođđogeađggit  vuođđo#geađgi+N+Pl+Nom
! into
!    vuođđogeađggit  vuođđu+N+SgNomCmp#geađgi+N+Pl+Nom
!    vuođđogeađggit  vuođđogeađgi+N+Pl+Nom
! and
!    sámegiela       sápmi+SgGenCmp+N+SgGenCmp#giella+N+Sg+Gen
!    sámegiela       sápmi+SgGenCmp+N+SgGenCmp#giella+N+Sg+Acc
!    sámegiela       sápmi+SgGenCmp+PlGenCmp+N+SgGenCmp#giella+N+Sg+Gen
!    sámegiela       sápmi+SgGenCmp+PlGenCmp+N+SgGenCmp#giella+N+Sg+Acc
! into
!    sámegiela       sápmi+N+SgGenCmp#giella+N+Sg+Gen
!    sámegiela       sápmi+N+SgGenCmp#giella+N+Sg+Acc

Alphabet

%#:0
%# ! Need this, since otherwise #-words not matched by our rule will disappear

%+N
%+A

%+Cmp
%+ShCmp
%+Cmpnd
%+SgCmp      	%+PlCmp
%+SgLeft      	%+PlLeft
%+SgNomCmp   	%+PlNomCmp
%+SgNomLeft  	%+PlNomLeft
%+SgGenCmp   	%+PlGenCmp
%+SgGenLeft  	%+PlGenLeft
%+DefSgGenCmp 	%+DefPlGenCmp

%+Cmpnd:0
%+SgCmp:0    	%+PlCmp:0
%+SgLeft:0    	%+PlLeft:0
%+SgNomCmp:0 	%+PlNomCmp:0
%+SgNomLeft:0  	%+PlNomLeft:0
%+SgGenCmp:0 	%+PlGenCmp:0
%+SgGenLeft:0  	%+PlGenLeft:0
%+DefSgGenCmp:0 %+DefPlGenCmp:0

;


Sets

Cmptag = 
%+Cmp
%+ShCmp
%+Cmpnd
%+SgCmp      	%+PlCmp
%+SgLeft      	%+PlLeft
%+SgNomCmp   	%+PlNomCmp
%+SgNomLeft  	%+PlNomLeft
%+SgGenCmp   	%+PlGenCmp
%+SgGenLeft  	%+PlGenLeft
%+DefSgGenCmp 	%+DefPlGenCmp
;

RemCmptag = 			! don't remove Cmp and ShCmp
%+Cmpnd
%+SgCmp      	%+PlCmp
%+SgLeft      	%+PlLeft
%+SgNomCmp   	%+PlNomCmp
%+SgNomLeft  	%+PlNomLeft
%+SgGenCmp   	%+PlGenCmp
%+SgGenLeft  	%+PlGenLeft
%+DefSgGenCmp 	%+DefPlGenCmp
;

KeepCmpAfter =			! could expand to include lemmas...
%+N
%+A
;

Rules 

"#-deletion unless after compounding tags"
! %#:0 <=> ~ [ Cmptag HashPrefix:? * ] _ ;           ! <--- this doesn't parse :(
%#:0 <=> \Cmptag _ ;  
! After inversion, we get
!    ^sámegiella/sápmi+N+SgGenCmp#giella+N+Sg+Nom/sáme#giella+N+Sg+Nom$
! After this file, we get
!    ^sámegiella/sápmi+N+SgGenCmp#giella+N+Sg+Nom/sámegiella+N+Sg+Nom$
! After substitution/relabelling, we get
!    ^sámegiella/sápmi<N><Sg><Gen><Cmp>+giella<N><Sg><Nom>/sámegiella<N><Sg><Nom>$
! which after pretransfer turns into 
!    ^sápmi<N><Sg><Gen><Cmp>$ ^giella<N><Sg><Nom>$
!    ^sámegiella<N><Sg><Nom>$

"Delete compound tags (except Cmp/ShCmp) that weren't followed by an N or A tag"
! Cmptag:0 <=> ~[ KeepCmpAfter %+Use/Sub:? * ] _ ;   ! <--- this doesn't parse :(
RemCmptag:0 <=> \KeepCmpAfter _ ; 
! We don't remove +Cmp or +ShCmp tags since they follow lemmas, not +N or +A:
! mielbargin      miel+ShCmp#bargat+V+TV+Der2+Actor+N+Ess
